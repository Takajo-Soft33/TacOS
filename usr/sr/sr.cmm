//#define DEBUG

#if defined(DEBUG)
# define debug(...) fprintf(stderr, __VA_ARGS__)
#else
# define debug(...) /* __VA_ARGS__ */
#endif

#include <stdio.hmm>
#include <stdlib.hmm>
#include <string.hmm>
#include <ctype.hmm>
#define N 81
char[] lbuf=array(N);

void bwrite(FILE fp, char[] bbuf, int bsize) {
  for (int i=0; i<bsize; i=i+1) {
    fputc(bbuf[i], fp);
  }
}

char[] xbuf = array(3);
char x2c(char[] xs, int offset) {
  xbuf[0] = xs[offset];
  xbuf[1] = xs[offset+1];
  xbuf[2] = '\0';
  return chr(htoi(xbuf));
}

public int main(int argc, char[][] argv) {
  if (argc>1) {
    debug("Usage: %s\n", argv[0]);
    return 1;
  }

  fgets(lbuf, N, stdin);
  int pos=strRchr(lbuf, '\n');
  if (pos==-1) {
    debug("format error");
    exit(1);
  }

  lbuf[pos] = '\0';

  debug("fopen:%s\n",lbuf);
  FILE fp = fopen(lbuf, "w");
  if (fp==null) {
    perror(lbuf);
    exit(1);
  }

  fgets(lbuf, N, stdin);
  int fsize = atoi(lbuf);
  debug("filelength:%s:%d\n",lbuf,fsize);

  fgets(lbuf, N, stdin);
  int bsize = atoi(lbuf);
  debug("blocksize:%s:%d\n",lbuf,bsize);

  char[] bbuf = malloc(bsize);

  int cbsize = 0;
  int cfsize = 0;
  while (cfsize<fsize) {
    fgets(lbuf, N, stdin);
    /// [入力のパース]
    int llen = strLen(lbuf) - 1;
    for (int k = 0; k < llen; k = k + 2) {
      if(!isXdigit(lbuf[k]) && !isXdigit(lbuf[k+1])) continue;
      bbuf[cbsize] = x2c(lbuf, k);
      cbsize = cbsize + 1;
      cfsize = cfsize + 1;
    }

    debug("file: %d / %d, buf: %d / %d\n", cfsize, fsize, cbsize, bsize);

    if(cbsize > bsize || cfsize > fsize) {
      printf("!overflow!\n");
      exit(1);
    }
    /// [/入力のパース]

    if (cbsize == bsize || cfsize == fsize) {
      bwrite(fp, bbuf, cbsize);
      cbsize = 0;
      printf("!Ack\n");
    }

  }
  fclose(fp);

  return 0;
}
